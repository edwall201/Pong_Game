/**
 * READ THIS DESCRIPTION!
 *
 * The processor takes in several inputs from a skeleton file.
 *
 * Inputs
 * clock: this is the clock for your processor at 50 MHz
 * reset: we should be able to assert a reset to start your pc from 0 (sync or
 * async is fine)
 *
 * Imem: input data from imem
 * Dmem: input data from dmem
 * Regfile: input data from regfile
 *
 * Outputs
 * Imem: output control signals to interface with imem
 * Dmem: output control signals and data to interface with dmem
 * Regfile: output control signals and data to interface with regfile
 *
 * Notes
 *
 * Ultimately, your processor will be tested by subsituting a master skeleton, imem, dmem, so the
 * testbench can see which controls signal you active when. Therefore, there needs to be a way to
 * "inject" imem, dmem, and regfile interfaces from some external controller module. The skeleton
 * file acts as a small wrapper around your processor for this purpose.
 *
 * You will need to figure out how to instantiate two memory elements, called
 * "syncram," in Quartus: one for imem and one for dmem. Each should take in a
 * 12-bit address and allow for storing a 32-bit value at each address. Each
 * should have a single clock.
 *
 * Each memory element should have a corresponding .mif file that initializes
 * the memory element to certain value on start up. These should be named
 * imem.mif and dmem.mif respectively.
 *
 * Importantly, these .mif files should be placed at the top level, i.e. there
 * should be an imem.mif and a dmem.mif at the same level as process.v. You
 * should figure out how to point your generated imem.v and dmem.v files at
 * these MIF files.
 *
 * imem
 * Inputs:  12-bit address, 1-bit clock enable, and a clock
 * Outputs: 32-bit instruction
 *
 * dmem
 * Inputs:  12-bit address, 1-bit clock, 32-bit data, 1-bit write enable
 * Outputs: 32-bit data at the given address
 *
 */
module processor(
    // Control signals
    clock,                          // I: The master clock
    reset,                          // I: A reset signal

    // Imem
    address_imem,                   // O: The address of the data to get from imem
    q_imem,                         // I: The data from imem

    // Dmem
    address_dmem,                   // O: The address of the data to get or put from/to dmem
    data,                           // O: The data to write to dmem
    wren,                           // O: Write enable for dmem
    q_dmem,                         // I: The data from dmem

    // Regfile
    ctrl_writeEnable,               // O: Write enable for regfile
    ctrl_writeReg,                  // O: Register to write to in regfile
    ctrl_readRegA,                  // O: Register to read from port A of regfile
    ctrl_readRegB,                  // O: Register to read from port B of regfile
    data_writeReg,                  // O: Data to write to for regfile
    data_readRegA,                  // I: Data from port A of regfile
    data_readRegB                   // I: Data from port B of regfile
);
    // Control signals
    input clock, reset;

    // Imem
    output [11:0] address_imem;
    input [31:0] q_imem;

    // Dmem
    output [11:0] address_dmem;
    output [31:0] data;
    output wren;
    input [31:0] q_dmem;

    // Regfile
    output ctrl_writeEnable;
    output [4:0] ctrl_writeReg, ctrl_readRegA, ctrl_readRegB;
    output [31:0] data_writeReg;
    input [31:0] data_readRegA, data_readRegB;
	 
	 /* YOUR CODE STARTS HERE */
	 
	 // 32-bit register made from 32 DFFEs
	 wire [31:0] pc_in, pc_out;
	 genvar i;
	 generate
	   for (i = 0; i < 32; i = i + 1) begin: pc_reg
		  my_dffe pc_ff(
			 .q(pc_out[i]),
			 .d(pc_in[i]),
			 .clk(clock),
			 .en(1'b1),     // always enabled
			 .clr(reset)    // reset clears PC to 0
		  );
	   end
	 endgenerate
	 
    // Extract instruction fields
	 wire [4:0] opcode = q_imem[31:27];
	 wire [4:0] rd = q_imem[26:22];
	 wire [4:0] rs = q_imem[21:17];
	 wire [4:0] rt = q_imem[16:12];
	 wire [4:0] shamt = q_imem[11:7];
	 wire [4:0] funct = q_imem[6:2];
	 wire [16:0] imm = q_imem[16:0];
	 wire [31:0] imm_ext = {{15{imm[16]}}, imm};	
	 wire [26:0] t = q_imem[26:0];
	 wire [31:0] t_ext = {5'b00000, t};

	 // Determine type	 
	 wire is_R, is_add, is_addi, is_sub, is_sw, is_lw, is_bne, is_blt, is_j, is_jal, is_jr, is_bex, is_setx, is_beq;
	 opcode_decode op0(opcode, is_R, is_addi, is_sw, is_lw, is_bne, is_blt, is_j, is_jal, is_jr, is_bex, is_setx, is_beq);
	 assign is_add = is_R & (~funct[4]) & (~funct[3]) & (~funct[2]) & (~funct[1]) & (~funct[0]);
	 assign is_sub = is_R & (~funct[4]) & (~funct[3]) & (~funct[2]) & (~funct[1]) & funct[0];
	 
	 // Compute PC + 1 + N
	 wire [31:0] branch_target;
	 alu alu_compute_branch (
		  .data_operandA(32'b1),
		  .data_operandB(imm_ext),
		  .ctrl_ALUopcode(5'b00000), // ADD
		  .ctrl_shiftamt(5'b00000),
		  .data_result(branch_target),
		  .isEqualTo(),
		  .isNotEqual(),
		  .isLessThan(),
		  .overflow()
	 );

	 wire alu_e, alu_ne, alu_lt, alu_ovf;
	 wire do_branch = (is_bne & alu_ne) | (is_blt & alu_lt) | (is_beq & alu_e);
	 wire [31:0] next_instruct = do_branch ? branch_target : 32'b1;
	 
	 // PC Register
	 // Either PC + 1 or PC + 1 + N
	 wire [31:0] nextPC;
	 alu alu_pc (
		 .data_operandA(pc_out),
		 .data_operandB(next_instruct),
		 .ctrl_ALUopcode(5'b00000), // ADD
		 .ctrl_shiftamt(5'b00000),
		 .data_result(nextPC),
		 .isEqualTo(),
		 .isNotEqual(),
		 .isLessThan(),
		 .overflow()
	 );
	 
	 assign pc_in = is_jr ? data_readRegB : (is_j | is_jal | (is_bex & alu_ne)) ? t_ext : nextPC;
	 assign address_imem = pc_out[11:0];
	 
	 wire use_imm = is_addi | is_sw | is_lw;

	 // ALU instruction control
	 wire [4:0] alu_opcode = is_R ? funct : 5'b00000;
	 
	 // Execute ALU input and output control
	 wire [31:0] alu_inA = data_readRegA;
	 wire [31:0] alu_inB = use_imm ? imm_ext : data_readRegB;
	 wire [31:0] alu_result;
	
	 // Execute PC
	 alu alu0 (
	   .data_operandA(alu_inA),
	   .data_operandB(alu_inB),
	   .ctrl_ALUopcode(alu_opcode),
	   .ctrl_shiftamt(shamt),            
	   .data_result(alu_result),
		.isEqualTo(alu_e),
	   .isNotEqual(alu_ne),
	   .isLessThan(alu_lt),
	   .overflow(alu_ovf)
	 );
	 
	 // Overflow detection
	 wire ovf_add = alu_ovf & is_add;
	 wire ovf_addi = alu_ovf & is_addi;
	 wire ovf_sub = alu_ovf & is_sub;
	 wire has_ovf = ovf_add | ovf_addi | ovf_sub;
	 
	 assign address_dmem = alu_result[11:0];
	 assign data = data_readRegB;
	 assign wren = is_sw;
	 
	 wire wr_jal = is_jal & ~has_ovf;
	 wire wr_setx = is_setx & ~has_ovf;
	 wire wr_norm = (is_R | is_addi | is_lw) & ~wr_jal & ~wr_setx & ~has_ovf;
	 
	 // Regfile connections
	 assign ctrl_readRegA = is_bex ? 5'd30 : is_blt ? rd : rs;
	 assign ctrl_readRegB = is_bex ? 5'd0 : is_blt ? rs : (is_bne | is_sw | is_jr | is_beq) ? rd : rt;
	 assign ctrl_writeEnable = has_ovf | wr_jal | wr_setx | wr_norm;
	 
	 wire [31:0] wb_normal = is_lw ? q_dmem : alu_result;
	 wire [31:0] wb_status = ovf_add ? 32'd1 : ovf_addi ? 32'd2 : 32'd3;
	 assign data_writeReg = has_ovf ? wb_status : wr_setx ? t_ext : wr_jal ? nextPC : wb_normal;
	 assign ctrl_writeReg = has_ovf ? 5'd30 : wr_setx ? 5'd30 : wr_jal ? 5'd31 : rd;
endmodule
